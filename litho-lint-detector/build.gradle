apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

configurations {
  lintChecks
}

dependencies {
  implementation deps.lint.api
  implementation deps.lint.checks
  lintChecks files(jar)
  implementation deps.kotlin.stdLibJre8

  testImplementation deps.lint.core
  testImplementation deps.lint.tests
}

jar {
  manifest {
    attributes('Lint-Registry': 'com.github.pavlospt.LithoIssueRegistry')
  }
}

// Related to https://issuetracker.google.com/issues/38454212
task copyTestClasses(type: Copy) {
  from "build/classes/kotlin"
  into "build/classes/java"
}

afterEvaluate {
  testClasses.dependsOn copyTestClasses
}
//

// Runs the copyLintJar task after assemble has completed.
task install(type: Copy, dependsOn: assemble) {
  from configurations.lintChecks
  into System.getProperty('user.home') + '/.android/lint/'
}

assemble.finalizedBy(install)
//

defaultTasks 'assemble'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath deps.kotlin.gradlePlugin
  }
}
repositories {
  mavenCentral()
  maven {
    url "https://maven.google.com"
  }
}
