buildscript {
  ext.versions = ['minSdk'          : 15,
                  'compileSdk'      : 26,
                  'targetSdk'       : 26,
                  'buildTools'      : '26.0.2',
                  'supportLibrary'  : '26.1.0',
                  'constraintLayout': '1.0.2',
                  'soLoader'        : '0.2.0',
                  'litho'           : '0.7.0',
                  'androidPlugin'   : '3.0.0',
                  'androidTools'    : '26.0.0',
                  'kotlin'          : '1.1.51']

  ext.deps = [android : ['gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}"],
              support : ['compat'          : "com.android.support:support-compat:${versions.supportLibrary}",
                         'appcompatV7'     : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                         'annotations'     : "com.android.support:support-annotations:${versions.supportLibrary}",
                         'constraintLayout': "com.android.support.constraint:constraint-layout:${versions.constraintLayout}"],
              lint    : ['core'  : "com.android.tools.lint:lint:${versions.androidTools}",
                         'api'   : "com.android.tools.lint:lint-api:${versions.androidTools}",
                         'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
                         'tests' : "com.android.tools.lint:lint-tests:${versions.androidTools}"],
              kotlin  : ['stdLibJre8'  : "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",
                         'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"],
              facebook: ['soLoader'        : "com.facebook.soloader:soloader:${versions.soLoader}",
                         'lithoWidget'     : "com.facebook.litho:litho-widget:${versions.litho}",
                         'lithoAnnotations': "com.facebook.litho:litho-annotations:${versions.litho}",
                         'lithoProcessor'  : "com.facebook.litho:litho-processor:${versions.litho}"]]

  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath deps.android.gradlePlugin
  }
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    google()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

// Related to https://issuetracker.google.com/issues/38454212
subprojects { subproject ->
  afterEvaluate {
    if (subproject.plugins.hasPlugin("kotlin")) {
      subproject.kotlin.copyClassesToJavaOutput = true
      subproject.jar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
  }
}
