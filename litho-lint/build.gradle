apply plugin: 'com.android.library'

android {
  compileSdkVersion 26
  buildToolsVersion "25.0.3"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26
    versionCode 2
    versionName "1.1"
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
}

configurations {
  lintChecks
}

dependencies {
  lintChecks project(path: ':litho-lint-detector', configuration: 'lintChecks')
}

tasks.withType(Javadoc).all { enabled = false }

task copyLintJar(type: Copy) {
  from(configurations.lintChecks) {
    rename { 'lint.jar' }
  }
  into 'build/intermediates/lint/'
}

project.afterEvaluate {
  def compileLintTask = project.tasks.find { it.name == 'compileLint' }
  compileLintTask.dependsOn(copyLintJar)
}

ext {
  PUBLISH_GROUP_ID = 'com.github.pavlospt'
  PUBLISH_ARTIFACT_ID = 'litho-lint'
  PUBLISH_VERSION = '1.1'
}

apply from: 'https://raw.githubusercontent.com/ArthurHub/release-android-library/master/android-release-aar.gradle'

//apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
