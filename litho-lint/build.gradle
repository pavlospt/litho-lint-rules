apply plugin: 'java'

def logger = new com.android.build.gradle.internal.LoggerWrapper(project.logger)
def sdkHandler = new com.android.build.gradle.internal.SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
  repositories.maven {
    url = file.toURI()
  }
}

configurations {
  lintChecks
}

dependencies {
  compile 'com.android.tools.lint:lint-api:25.3.0'
  compile 'com.android.tools.lint:lint-checks:25.3.0'
  lintChecks files(jar)
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
  jcenter()
  maven {
    url "https://repo1.maven.org/maven2/"
  }
}

jar {
  manifest {
    attributes('Lint-Registry': 'com.github.pavlospt.LithoIssueRegistry')
  }
}

defaultTasks 'assemble'

task copyLintJar(type: Copy) {
  description = 'Copies the lint jar file into the {user.home}/.android/lint folder.'
  from('build/libs/')
  into(System.getProperty("user.home") + '/.android/lint')
  include("*.jar")
}

assemble.finalizedBy(copyLintJar)