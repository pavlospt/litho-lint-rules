apply plugin: 'com.android.library'

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode 3
    versionName "1.2"
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
  buildToolsVersion '26.0.2'
}

configurations {
  lintChecks
}

dependencies {
  lintChecks project(path: ':litho-lint-detector', configuration: 'lintChecks')
}

tasks.withType(Javadoc).all { enabled = false }

task copyLintJar(type: Copy) {
  from(configurations.lintChecks) {
    rename { 'lint.jar' }
  }
  into 'build/intermediates/lint/'
}

project.afterEvaluate {
  def compileLintTask = project.tasks.find { it.name == 'compileLint' }
  compileLintTask.dependsOn(copyLintJar)
}

ext {
  PUBLISH_GROUP_ID = 'com.github.pavlospt'
  PUBLISH_ARTIFACT_ID = 'litho-lint'
  PUBLISH_VERSION = '1.2'
}

//apply from: 'https://raw.githubusercontent.com/ArthurHub/release-android-library/master/android-release-aar.gradle'

//apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
